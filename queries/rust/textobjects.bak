; TextObjects for Rust PreProcs

(macro_definition) @meta.outer

(macro_definition
  (macro_rule)) @meta.inner

(attribute_item) @meta.outer

(attribute_item
  (attribute
    arguments: (token_tree
      .
      [
        "("
        "["
        "{"
      ]
      .
      (_) @_start
      (_)? @_end
      .
      [
        ")"
        "]"
        "}"
      ]
      (#make-range! "meta.inner" @_start @_end))))

(inner_attribute_item) @meta.outer

(inner_attribute_item
  attribute: (attribute
    (token_tree
      .
      [
        "("
        "["
        "{"
      ]
      .
      (_) @_start @_end
      (_)? @_end
      .
      [
        ")"
        "]"
        "}"
      ]
      (#make-range! "meta.inner" @_start @_end))))

(inner_attribute_item
  attribute: (attribute
    delimited_attribute: (attribute
      (token_tree
        .
        [
          "("
          "["
          "{"
        ]
        .
        (_) @_start @_end
        (_)? @_end
        .
        [
          ")"
          "]"
          "}"
        ]
        (#make-range! "meta.inner" @_start @_end)))))
